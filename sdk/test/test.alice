### test utility ###

"io.alice" include

export var null:running-test

struct {
    string file,
    string current,
    number nfailed,
    number ifailed,
    number total
}:Test

export fun { (string file) args
    if (running-test ?null not) {
        running-test.finish
    } fi
    file "" 0 0 0 Test :running-test
    "running tests for "P file Pln
    running-test
}:test-file

implement Test {
    fun { (string name) args
        name self>current
        self.total ++ self>total
    }:test

    fun {
        if (self.ifailed) {
            "  "P self.current P " failed!"Pln
            0 self>ifailed
            self.nfailed ++ self>nfailed
        } else {
            "    "P self.current P " passed!"Pln
        } fi
    }:done

    fun {
        if (self.nfailed 0 eq) {
            "... all "P self.total P " tests passed!"Pln
        } else {
            "... "P self.nfailed P " of "P self.total P " tests failed!"Pln
        } fi
    }:finish
} end

export fun {
    if (not) {
        1 running-test>ifailed
    } fi
}:assert

export fun {
    eq assert
}:asserteq
